# Error Handling

This guide explains how errors are handled in the Hermes MCP client.

## Error Types

Hermes MCP distinguishes between three types of errors:

1. **Protocol errors** (JSON-RPC errors): Standard errors defined by the JSON-RPC 2.0 specification
2. **Domain errors**: Application-level errors reported by the server with `isError: true`
3. **Client-side errors**: Errors generated by the client before reaching the server

## Error Representation

All errors in Hermes MCP are represented as `{:error, %Hermes.MCP.Error{}}` tuples, where `Error` is a struct with:

- `code`: The numeric error code
- `reason`: The atom reason (e.g., `:parse_error`, `:method_not_found`, `:domain_error`)
- `data`: Additional error context

### Protocol Errors

Protocol errors (JSON-RPC errors) use the standard JSON-RPC error codes with specific reason atoms:

```elixir
{:error, %Hermes.MCP.Error{
  code: -32601,
  reason: :method_not_found,
  data: %{original_message: "Method not found"}
}}
```

Standard JSON-RPC error codes:
- `-32700`: Parse error (`:parse_error`)
- `-32600`: Invalid request (`:invalid_request`)
- `-32601`: Method not found (`:method_not_found`)
- `-32602`: Invalid params (`:invalid_params`)
- `-32603`: Internal error (`:internal_error`)
- `-32000`: Server error (`:server_error`)

### Domain Errors

Domain errors (application-level errors) use the `:domain_error` reason, with the original response data preserved:

```elixir
{:error, %Hermes.MCP.Error{
  code: -32000,
  reason: :domain_error,
  data: %{
    "isError" => true,
    "reason" => "tool_not_found",
    "message" => "Tool 'unknown' not found",
    "details" => %{"toolName" => "unknown"}
  }
}}
```

### Client-Side Errors

Client-specific errors follow the same error struct pattern with appropriate reason atoms:

```elixir
# Transport error
{:error, %Hermes.MCP.Error{
  code: -32000,
  reason: :connection_refused,  
  data: %{type: :transport}
}}

# Capability error
{:error, %Hermes.MCP.Error{
  code: -32601,
  reason: :method_not_found,
  data: %{method: "resources/list"}
}}

# Timeout error
{:error, %Hermes.MCP.Error{
  code: -32000, 
  reason: :request_timeout, 
  data: %{type: :client, message: "Request timed out after 30000ms"}
}}
```

## Handling Errors

The client automatically detects and categorizes errors, returning them in consistent formats.

You can pattern match on the error structure to handle different error types:

```elixir
case Hermes.Client.call_tool("search", %{query: "example"}) do
  {:ok, result} ->
    # Handle success
    handle_success(result)
    
  {:error, %Hermes.MCP.Error{reason: :domain_error, data: data}} ->
    # Handle domain/application error
    case data do
      %{"reason" => "not_found"} -> 
        Logger.warning("Resource not found: #{data["message"]}")
      
      %{"reason" => "permission_denied"} ->
        Logger.error("Permission denied: #{data["message"]}")
        
      _ ->
        Logger.error("Other domain error: #{inspect(data)}")
    end
    
  {:error, %Hermes.MCP.Error{reason: :method_not_found}} ->
    # Handle unsupported method
    Logger.warning("Method not supported by this server")
    
  {:error, %Hermes.MCP.Error{reason: reason}} when reason in [:connection_refused, :timeout] ->
    # Handle transport errors
    Logger.error("Transport error: #{reason}")
    
  {:error, %Hermes.MCP.Error{} = error} ->
    # Handle any other error
    Logger.error("Unexpected error: #{inspect(error)}")
end
```

## Error Inspection

For better debugging, errors implement a custom Inspect protocol:

```
#MCP.Error<method_not_found %{method: "unknown_method"}>
#MCP.Error<domain_error %{"isError" => true, "reason" => "not_found", "message" => "Resource not found"}>
```

## Creating Custom Errors

If you need to create your own errors in your application:

```elixir
# Protocol errors
Hermes.MCP.Error.parse_error()
Hermes.MCP.Error.method_not_found(%{method: "unknown_method"})

# Transport errors
Hermes.MCP.Error.transport_error(:connection_refused)

# Client errors
Hermes.MCP.Error.client_error(:request_timeout, %{elapsed_ms: 30000})

# Domain errors
Hermes.MCP.Error.domain_error(%{"reason" => "not_found", "message" => "Resource not found"})
```
